PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX : <http://model.geneontology.org/>

SELECT ?nonprod ?production ?allnoctua (?nonprod + ?production as ?computedtotal)
WHERE {
  {
    SELECT (COUNT(?nps) as ?nonprod) WHERE {
      GRAPH ?npg {
        ?npg :graphType :noctuaCam .
        ?nps ?npp ?npo  .
      }
      FILTER NOT EXISTS {
        {
          GRAPH ?npg {
            ?npg <http://geneontology.org/lego/modelstate> "production"^^xsd:string .
            ?nps ?npp ?npo  .
          }
        } UNION {
          GRAPH ?npg {
            ?npg prov:wasDerivedFrom ?asserted .
            ?nps ?npp ?npo
          }
          GRAPH ?asserted {
            ?asserted <http://geneontology.org/lego/modelstate> "production"^^xsd:string .
            ?nps1 ?npp1 ?npo1
          }
        }
      }
    }
  }
  {
    SELECT (COUNT(?ps) as ?production) WHERE {

      {
        GRAPH ?prodg {
          ?prodg <http://geneontology.org/lego/modelstate> "production"^^xsd:string .
          ?ps ?pp ?po .
        }
      } UNION {
        GRAPH ?prodg {
          ?prodg prov:wasDerivedFrom ?asserted .
          ?ps ?pp ?po .
        }
        GRAPH ?asserted {
          ?asserted <http://geneontology.org/lego/modelstate> "production"^^xsd:string .
          ?ps1 ?pp1 ?po1 .
        }
      }
    }
  }
  {
    SELECT (COUNT(?alls) as ?allnoctua) WHERE {
      GRAPH ?allg {
        ?allg :graphType :noctuaCam .
        ?alls ?allp ?allo .
      }
    }
  }
}
